<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4019.2">
  <POU Name="M_ACTUATION_EXT_HOMING" Id="{62088918-25f8-449a-948f-d62393bd2c8b}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK M_ACTUATION_EXT_HOMING EXTENDS M_ACTUATION
VAR_INPUT
END_VAR
VAR_OUTPUT
END_VAR
VAR
	_HardStopCalibration	: BOOL := FALSE;
	_HardStopTol			: LREAL := 5.0;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Methods" Id="{0925be61-b6c1-4054-9923-72b6a8a7094d}" />
    <Folder Name="Homing Properties" Id="{3646a56a-24ea-4e69-9db1-ac91a9ed4989}" />
    <Method Name="Home" Id="{0df8e827-fad5-40c8-9f9a-41319b3a55bf}" FolderPath="Methods\">
      <Declaration><![CDATA[{attribute '''NORMAL__COMMENT' := '
	Y:2016		M:05	D:20
	D: Initiates the homing sequence for the actuation device. Configure the homing behavior via the class's properties.
'}
(*
	Y:2016		M:05	D:20
	D: Initiates the homing sequence for the actuation device. Configure the homing behavior via the class's properties.
*)
(*
	Y:2016		M:06	D:03
	D: Initiates the homing sequence for the actuation device. Configure the homing behavior via the class's properties.
	EXT: Uses the Use_Hard_Stop option to calibrate the homing sequence
*)
METHOD Home : DINT
VAR_INPUT
	CalibrateAtLimit : BOOL;
END_VAR
VAR
	_CALIBRATION_FLAG : BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT CalibrateAtLimit THEN
	IF _HardStopCalibration THEN
		BypassLimit();
		_CALIBRATION_FLAG := ABS(SUPER^._DATALINK.NC_INTERFACE.NcToPlc.PosDiff) > _HardStopTol;
	ELSE
		_CALIBRATION_FLAG := SUPER^._DATALINK.CALIBRATION_SENSOR;
	END_IF
ELSE 
	BypassLimit();
	IF SUPER^.CalibrationAtMinus THEN
		_CALIBRATION_FLAG := SUPER^._DATALINK.LIMIT_MINUS XOR _NORMCLOSEDLIMIT;
	ELSE
		_CALIBRATION_FLAG := SUPER^._DATALINK.LIMIT_PLUS XOR _NORMCLOSEDLIMIT;
	END_IF
END_IF
_M_HOME(
	Axis:= SUPER^._DATALINK.NC_INTERFACE, 
	Execute:= TRUE, 
	bCalibrationCam:= _CALIBRATION_FLAG);
IF _M_HOME.Error THEN
	EnableLimits();
	_M_HOME(
		Axis:= SUPER^._DATALINK.NC_INTERFACE, 
		Execute:= FALSE);
	Home := 1;
	RETURN;
ELSIF _M_HOME.CommandAborted THEN
	EnableLimits();
	_M_HOME(
		Axis:= SUPER^._DATALINK.NC_INTERFACE, 
		Execute:= FALSE);
	Home := 2;
	RETURN;
ELSIF _M_HOME.Done THEN
	EnableLimits();
	_M_HOME(
		Axis:= SUPER^._DATALINK.NC_INTERFACE, 
		Execute:= FALSE);
	Home := -1;
	RETURN;
ELSE
	Home := 0;
	RETURN;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Property Name="CalibrationOnHardstop" Id="{412a5baf-f17a-40f0-af97-0039df390cf2}" FolderPath="Homing Properties\">
      <Declaration><![CDATA[PROPERTY CalibrationOnHardstop : BOOL]]></Declaration>
      <Set Name="Set" Id="{85472196-5565-417c-a8d3-5fe2a67fc4f6}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_HardStopCalibration := CalibrationOnHardstop;]]></ST>
        </Implementation>
      </Set>
      <Get Name="Get" Id="{ac8e0e82-dcb2-48ed-8573-89499a94a8d6}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[CalibrationOnHardstop := _HardStopCalibration;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="HardstopTolerance" Id="{822e561a-7843-42be-b6e8-c04f6f6e8e90}" FolderPath="Homing Properties\">
      <Declaration><![CDATA[PROPERTY HardstopTolerance : LREAL]]></Declaration>
      <Get Name="Get" Id="{0baa77f0-a0dc-4bc3-9e2f-924e61327bd3}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[HardstopTolerance := _HardStopTol;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{1d023def-74b9-4e38-a93c-a9fb27cd61e9}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[ _HardStopTol := HardstopTolerance;]]></ST>
        </Implementation>
      </Set>
    </Property>
  </POU>
</TcPlcObject>